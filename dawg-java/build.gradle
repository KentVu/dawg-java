plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

apply plugin: 'kotlin'

group rootProject.group
version rootProject.version

sourceCompatibility = 1.8

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    //implementation project(":dawg-jni")
    //testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation "io.mockk:mockk:1.9.3"
    testImplementation 'io.kotlintest:kotlintest-runner-junit5:3.4.2'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
}

// For gradle >=6
//java {
//    //withJavadocJar()
//    withSourcesJar()
//}

test {
    systemProperty "java.library.path", file("${project(":dawg-jni").buildDir}/lib/main/debug").absolutePath
    useJUnitPlatform()
    // https://stackoverflow.com/a/41107537/1562087
    testLogging {
        outputs.upToDateWhen {false}
        showStandardStreams = true
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
}

task copyLib(type: Copy) {
    from file("${project(":dawg-jni").buildDir}/lib/main/debug").absolutePath
    into 'src/main/resources'
}

copyLib.dependsOn ':dawg-jni:linkDebug'
compileKotlin.dependsOn copyLib

clean.dependsOn rootProject.cleanSwig

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/kentvu/dawg-java")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("PASSWORD")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            artifact sourcesJar
            from(components.java)
        }
    }
}

signing {
    sign publishing.publications.gpr
}
